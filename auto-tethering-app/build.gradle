apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'jacoco-android'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

android {
    signingConfigs {
        release
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "GOOGLE_API_KEY", getMyApiKey("GOOGLE_API_KEY_RELEASE")
        }
        debug {
            debuggable true
            buildConfigField "String", "GOOGLE_API_KEY", getMyApiKey("GOOGLE_API_KEY_DEBUG")
        }
        coverage {
            debuggable true
            testCoverageEnabled = true
            buildConfigField "String", "GOOGLE_API_KEY", getMyApiKey("GOOGLE_API_KEY_DEBUG")
        }
    }

    compileSdkVersion 22
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.labs.dm.auto_tethering"
        minSdkVersion 9
        maxSdkVersion 23
        targetSdkVersion 21
        versionCode 68
        versionName "0.0.68"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + getDateAsMillis() + "L)"
        buildConfigField "String", "GOOGLE_API_KEY", getMyApiKey("GOOGLE_API_KEY")
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

static getDateAsMillis() {
    Calendar cal = Calendar.getInstance()
    return cal.getTimeInMillis().toString()
}

dependencies {
    //compile -->
    compile fileTree(include: ['*.jar'], dir: 'libs')
//    compile 'com.google.dagger:dagger:2.9'
//    annotationProcessor 'com.google.dagger:dagger-compiler:2.9'
//    compile 'com.jakewharton:butterknife:8.5.1'
//    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2@aar'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }
    //testCompile -->
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.14'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.robolectric:robolectric:3.3.1'
    //androidTestCompile -->
    //androidTestCompile 'com.android.support.test:runner:0.5'
    //androidTestCompile 'com.android.support.test:rules:0.5'
    //androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'org.mockito:mockito-core:2.7.14'
}

Properties props = new Properties()
def propFile = new File(System.properties['user.home'] + '/.gradle/gradle.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props && props.containsKey('RELEASE_STORE_FILE') && props.containsKey('RELEASE_STORE_PASSWORD') &&
            props.containsKey('RELEASE_KEY_ALIAS') && props.containsKey('RELEASE_KEY_PASSWORD')) {
        println 'gradle.properties found at:  ' + propFile
        android.signingConfigs.release.with {
            storeFile = file(System.properties['user.home'] + '/.gradle/' + props['RELEASE_STORE_FILE'])
            storePassword = props['RELEASE_STORE_PASSWORD']
            keyAlias = props['RELEASE_KEY_ALIAS']
            keyPassword = props['RELEASE_KEY_PASSWORD']
        }
    } else {
        println 'gradle.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'gradle.properties not found'
    android.buildTypes.release.signingConfig = null
}

def getMyApiKey(String property) {
    def keyFile = new File("$project.rootDir/api.key")
    if (keyFile.canRead()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(keyFile))
        return "\"" + properties.getProperty(property) + "\""
    } else {
        println 'the file: api.key not found'
        return "\"\""
    }
}

ext {
    ANDROID_BUILD_MIN_SDK_VERSION = 9
    ANDROID_BUILD_TARGET_SDK_VERSION = 25
    ANDROID_BUILD_TOOLS_VERSION = '25.0.2'
    ANDROID_BUILD_SDK_VERSION = 25
    SUPPORT_VERSION = '25.0.0'
    MAVEN_VERSION = '1.1.3'
}
